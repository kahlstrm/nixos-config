echo "# GENERATED BY create-extended-git.zsh\n"
echo "# additional git aliases \n"

typeset -A ALIASES=(
  [grsp]="git restore --patch"
  [grssp]="git restore --source --patch"
  [grstp]="git restore --staged --patch"
  [gwips]='git commit --no-verify --no-gpg-sign --message "--wip-- [skip ci]"'
  [grbi2]="git rebase --interactive HEAD~2"
  [grbi3]="git rebase --interactive HEAD~3"
  [grbi4]="git rebase --interactive HEAD~4"
  [grbi5]="git rebase --interactive HEAD~5"
)

for key in "${(@k)ALIASES}"; do
  alias "$key=${ALIASES[$key]}"
  echo alias "$key='${ALIASES[$key]}'"
done

echo "\n# dotfiles aliases generated by git aliases\n"


for alias_name in ${(k)aliases}; do
  [[ "${aliases[$alias_name]}" =~ "^git " || "$alias_name" == "grt" ]] || continue
  new_alias="$(
    echo "${aliases[$alias_name]}" \
    | sed -e 's/git /dotfiles /g')"
  echo alias "c${alias_name}"=\'"$new_alias"\'
done
